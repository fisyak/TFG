*
* $Id: dc_medi.F,v 1.4 1997/09/27 16:57:57 fisyak Exp $
*
* $Log: dc_medi.F,v $
c Revision 1.4  1997/09/27  16:57:57  fisyak
c Clean up
c
c Revision 1.3  1997/08/07  18:14:51  fisyak
c clean up
c
c Revision 1.2  1997/05/15  17:37:47  fisyak
c use for ABSL geisha or fluka
c
c Revision 1.1.1.1  1997/03/12  01:49:28  fisyak
c d0gstar import in d0cvs
c
* Revision 1.1.1.1  1997/01/24 02:54:17  fisyak
* The first D0GSTAR version
*
*
      SUBROUTINE DC_MEDI
*
************************************************************************
*
*DOC   Checks if the cross section table matches with the data cards
*DOC   supplied by the user
*
*DOC   Arguments :  None
*
*DOC   Called by DCGEOM
*
*DOC   Created:     15-DEC-1995   Yuri Fisyak
*
************************************************************************
*
#include "geant321/gcbank.inc"
#include "geant321/gccuts.inc"
#include "geant321/gcflag.inc"
#include "geant321/gclist.inc"
#include "geant321/gcmulo.inc"
#include "geant321/gconsp.inc"
#include "geant321/gcphys.inc"
#include "geant321/gctmed.inc"
#include "geant321/gcmate.inc"
      INTEGER         ILTAB, NICOLL
#include "geant321/gcstra.inc"
      INTEGER         MECA(5,13), MECAS(16), MECAG(16)
      REAL            CUTS(10), CUTG(10), PCUT(4), UCUT(10)
      EQUIVALENCE     (MECA(1,1), IPAIR)
      INTEGER         I, J, IFIEL, ITM, ITYPE, JTP
      INTEGER         MEC, IPHYSI, KLOS, IXST
      INTEGER         JTM, IMA, JMA
      REAL            EL, SIGENL
      REAL            FIELD
      REAL            BFIEL
      INTEGER         NTEMP
      REAL            GAMTT(21)
      SAVE            BFIEL
      DATA            BFIEL /40.0/
#if 0
*
      DATA GAMTT
     +  /     1.1 ,     1.2 ,     1.3 ,     1.5 ,     1.8 ,
     +        2.0 ,     2.5 ,     3.0 ,     4.0 ,     7.0 ,
     +       10.0 ,    20.0 ,    40.0 ,    70.0 ,   100.0 ,
     +      300.0 ,   600.0 ,  1000.0 ,  3000.0 , 10000.0 ,
     +    50000.0 /
*     ------------------------------------------------------------------
* Executable statements
*________________________
*
      DO I=1,21
         GAMLOG(I) = LOG(GAMTT(I))
      ENDDO
      IF (NEKBIN.LE.0.OR.NEKBIN.GT.199) NEKBIN=90
      IF (EKMIN.GE.EKMAX.OR.EKMIN.LE.0.) THEN
         EKMIN = 1.E-5
         EKMAX = 1.E+4
      ENDIF
      NEK1  = NEKBIN+1
      EKINV = 1./LOG10(EKMAX/EKMIN)
      EKBIN(1) = LOG10(EKMIN)
      ELOW(1)  = EKMIN
      GEKA     = NEKBIN*EKINV
      GEKB     = 1.-GEKA*EKBIN(1)
      DO 10 I=2,NEK1
         EL       = EKBIN(1)+(I-1)/GEKA
         EKBIN(I) = EL
         ELOW(I)  = 10.**EL
   10 CONTINUE
#endif
*
      IPHYSI = 0
      IF (JTMED.LE.0)           GOTO 999
*
* *** Load the cuts and physics processes to be used in this run
*
      CALL UCOPY (CUTGAM, CUTS, 10)
      DO I   = 1, 13
        MECAS(I) = MECA(1,I)
      END DO
      MECAS(14) = ILABS
      MECAS(15) = ISYNC
      MECAS(16) = ISTRA
*             If Landau fluctuations activated, cancel delta rays
      KLOS      = MECAS(11)
      IF (KLOS .EQ. 0) MECAS(5) = 0
      IF (KLOS .EQ. 2) THEN
        CUTS(8)  = 9999.
        CUTS(9)  = 9999.
        MECAS(5) = 0
      ENDIF
*
* *** If Cerenkov generation is on, activate Light absorbtion unless
*  ** explicitely switched off by the user
*
      IF (ITCKOV .NE. 0) THEN
        IF (ILABS .EQ. -1) THEN
          MECAS(14) = 1
        ENDIF
      ENDIF
      MECAS(14) = MAX(MECAS(14), 0)
*
*             If BCUTE,BCUTM,DCUTE,DCUTM,PPCUTM not initialized (=BIG)
*             Set them to CUTGAM,CUTGAM,CUTELE,CUTELE respectively
*
      IF (CUTS( 6) .GT. 0.9*BIG)  CUTS( 6) = CUTS(1)
      IF (CUTS( 7) .GT. 0.9*BIG)  CUTS( 7) = CUTS(1)
      IF (CUTS( 8) .GT. 0.9*BIG)  CUTS( 8) = CUTS(2)
      IF (CUTS( 9) .GT. 0.9*BIG)  CUTS( 9) = CUTS(2)
      IF (CUTS(10) .GT. 0.9*BIG)  CUTS(10) = 0.010
      IF (CUTS(10) .LT. 4.*EMASS) CUTS(10) = 4.*EMASS
*
* *** Standard TPAR read
*
      IF (IQ(JTMED-5) .NE. 0) THEN
        CALL UCOPY (Q(JTMED+1), CUTG, 10)
        DO I   = 1, 16
          IF (I .LE. 13) THEN
            MECAG(I) = Q(JTMED+I+10)
          ELSE
            MECAG(I) = Q(JTMED+I+17)
          ENDIF
        ENDDO
      ELSE
        IQ(JTMED-5) = 1
        CALL UCOPY (CUTS, CUTG, 10)
        CALL UCOPY (MECAS, MECAG, 16)
        IPHYSI = 1
      ENDIF
*
* *** Is parameters the same ?
*
      DO I = 1, 10
        IF (CUTS(I) .NE. CUTG(I))  IPHYSI = IPHYSI + 1
      ENDDO
      DO I = 1, 16
        IF (MECAS(I) .NE. MECAG(I)) IPHYSI = IPHYSI + 1
      ENDDO
      FIELD  = FCORUF * BFIEL
      IFIEL  = -1
      IF (FIELD.EQ.0.) IFIEL = 0
*
* *** Check the magnetic field index used for all the media
*
      DO 120 ITM = 1, IQ(JTMED-2)
        JTM = LQ(JTMED-ITM)
        IF (JTM.LE.0)              GO TO 120
        IF (Q(JTM+9) .EQ. 0.0)     GO TO 120
        IF (Q(JTM+9) .NE. FIELD)  THEN
          Q(JTM+9) = FIELD
          IF (IFIEL .EQ. 0) Q(JTM+8) = IFIEL
        ENDIF
  120 CONTINUE
*
* *** Put them back
*
      CALL UCOPY (CUTS, CUTGAM, 10)
      DO I   = 1, 13
        MECA(1,I) = MECAS(I)
      ENDDO
      ILABS = MECAS(14)
      ISYNC = MECAS(15)
      ISTRA = MECAS(16)
      CALL UCOPY (CUTS, Q(JTMED+1), 10)
      DO I   = 1, 16
        IF (I .LE. 13) THEN
          Q(JTMED+I+10) = MECAS(I)
        ELSE
          Q(JTMED+I+17) = MECAS(I)
        END IF
      END DO

      DO IMA = 1, IQ(JMATE-2)
        JMA = LQ(JMATE-IMA)
        IF (JMA .NE. 0) THEN
          DO J = 1, 18
            IF(LQ(JMA-J) .EQ. 0 .AND.
     &        J .NE. 5 .AND. J .NE. 12 .AND. J .NE. 14)
     &        IPHYSI = IPHYSI + 1
          END DO
*
* *** fix ABSL  for 10 GeV proton
*
          IF (IHADR .EQ. 1 .OR. IHADR .EQ. 3) THEN ! GEISHA 
            CALL GFTMAT(IMA, 14,'INEG',1,10.,SIGENL,PCUT,IXST)
          ELSE                                     ! FLUKA
            CALL GFTMAT(IMA, 14,'INEF',1,10.,SIGENL,PCUT,IXST)
          END IF
          IF (IXST .EQ. 1 .AND. SIGENL .GT. 0.0) THEN
            ABSL = 1./SIGENL
            Q(JMA+10) = ABSL
          END IF
        END IF
      END DO
      IF (IPHYSI .EQ. 0) GO TO 999
*
* *** Now check the cuts and the physics processes for special tracking media
*
      DO 260 ITM = 1, IQ(JTMED-2)
        JTM = LQ(JTMED-ITM)
        IF (JTM.LE.0)           GO TO 260
*
        IF (LQ(JTM).EQ.0)       THEN
          ITYPE = 1
          JTP   = JTMED
        ELSE
          ITYPE = 2
          JTP   = LQ(JTM)
        ENDIF
        IF (ITYPE .NE. 2) GO TO 260
*
        CALL UCOPY (Q(JTP+1), UCUT, 10)
        
        DO I = 1, 10
          IF (UCUT(I) .GT. CUTS(I)) Q(JTP+I) = CUTS(I)
        ENDDO
*
        DO I = 1, 16
          IF (I .LE. 13) THEN
            MEC    = Q(JTP+10+I)
            IF (MEC .EQ. MECAG(I)) Q(JTP+10+I) = MECAS(I)
          ELSE
            MEC    = Q(JTP+17+I)
            IF (MEC .EQ. MECAG(I)) Q(JTP+17+I) = MECAS(I)
          ENDIF
        ENDDO
  260 CONTINUE
*
* *** Print GEANT3 Header Box
*
      NTEMP  = NGET
      NGET   = 0
      CALL GPHYSI
      NGET   = NTEMP
*
  999 CONTINUE
*                                                            END DC_MEDI
      END
